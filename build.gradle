plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'com.flyerzrule.mc.guardutils'
version = '1.0.0'

def javaVersion = JavaVersion.VERSION_21
def encoding = 'UTF-8'
def paperVersion = '1.21.1-R0.1-SNAPSHOT'
def mcVersion = '1.21.1'

repositories {
    gradlePluginPortal()
    mavenCentral()
    maven {
        name 'papermc-repo'
        url 'https://papermc.io/repo/repository/maven-public/'
    }
    maven { 
        url 'https://jitpack.io' 
    }
    maven { 
        url "https://repo.dmulloy2.net/repository/public/"
    }
    maven {
        name 'xenondevs'
        url 'https://repo.xenondevs.xyz/releases'
    }
    maven {
        name 'roinujnosde-repo'
        url 'https://repo.roinujnosde.me/releases/'
    }
    maven {
        url "https://maven.pkg.github.com/KillionRevival/KillionCommons"
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
            password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
        }
    }
    maven {
        url "https://maven.pkg.github.com/KillionRevival/PacketWrapper"
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
            password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
        }
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:${paperVersion}"
    compileOnly "co.killionrevival:killioncommons:3.1-SNAPSHOT"
    compileOnly "net.luckperms:api:5.4"
    compileOnly 'com.mojang:authlib:1.5.25'

    compileOnly 'com.comphenix.protocol:ProtocolLib:5.3.0'
    compileOnly ('com.comphenix.packetwrapper:packetwrapper:1.20-2.2.1') {
        exclude group: 'org.spigotmc', module: 'spigot-api'
    }

    shadow 'xyz.xenondevs.invui:invui:1.43'

    compileOnly 'net.sacredlabyrinth.phaed.simpleclans:SimpleClans:2.19.2'

    compileOnly 'org.projectlombok:lombok:1.18.32'
    annotationProcessor 'org.projectlombok:lombok:1.18.32'
}

java {
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
}

tasks.withType(JavaCompile) {
    options.encoding = encoding
}

tasks.processResources {
    filesMatching('plugin.yml') {
        expand (version: project.version, mcVersion: mcVersion)
    }
}

tasks.assemble {
    dependsOn(tasks.shadowJar)
}

tasks.build {
    dependsOn(tasks.shadowJar)
}

shadowJar {
  configurations = [project.configurations.shadow]
  archiveBaseName.set(project.name)
  archiveVersion.set(project.version)
  archiveClassifier.set('')
}
