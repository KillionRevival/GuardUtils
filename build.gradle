plugins {
    id 'java'
    id 'maven-publish'
}

group = 'com.flyerzrule.mc.guardutils'
version = '1.1.0'

def javaVersion = JavaVersion.VERSION_21
def encoding = 'UTF-8'
def paperVersion = '1.21.4-R0.1-SNAPSHOT'
def mcVersion = '1.21.4'

repositories {
    gradlePluginPortal()
    mavenCentral()
    maven {
        name 'papermc-repo'
        url 'https://repo.papermc.io/repository/maven-public/'
    }
    maven { 
        url 'https://jitpack.io' 
    }
    maven { 
        url "https://repo.dmulloy2.net/repository/public/"
    }
    maven {
        name 'xenondevs'
        url 'https://repo.xenondevs.xyz/releases'
    }
    maven {
        name 'roinujnosde-repo'
        url 'https://repo.roinujnosde.me/releases/'
    }
    maven {
        name 'nightexpress-releases'
        url 'https://repo.nightexpressdev.com/releases'
    }
    maven {
        url "https://maven.pkg.github.com/KillionRevival/KillionCommons"
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
            password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
        }
    }
    maven {
        url "https://maven.pkg.github.com/KillionRevival/PacketWrapper"
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
            password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
        }
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:${paperVersion}"
    compileOnly "co.killionrevival:killioncommons:3.3.2-SNAPSHOT"
    compileOnly "net.luckperms:api:5.4"
    compileOnly 'com.mojang:authlib:1.5.25'

    compileOnly 'com.comphenix.protocol:ProtocolLib:5.3.0'
    compileOnly ('com.comphenix.packetwrapper:packetwrapper:1.20-2.2.1') {
        exclude group: 'org.spigotmc', module: 'spigot-api'
    }

    compileOnly 'xyz.xenondevs.invui:invui:1.43'

    compileOnly 'net.sacredlabyrinth.phaed.simpleclans:SimpleClans:2.19.2'

    compileOnly 'su.nightexpress.excellentcrates:ExcellentCrates:6.1.0'

    compileOnly 'org.projectlombok:lombok:1.18.32'
    annotationProcessor 'org.projectlombok:lombok:1.18.32'

    implementation "org.incendo:cloud-annotations:2.0.0"
    annotationProcessor "org.incendo:cloud-annotations:2.0.0"
    implementation "org.incendo:cloud-paper:2.0.0-beta.10"
    implementation("org.incendo:cloud-minecraft-extras:2.0.0-beta.10")
}

java {
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
}

tasks.withType(JavaCompile) {
    options.encoding = encoding
}

tasks.processResources {
    filesMatching(['plugin.yml', 'paper-plugin.yml']) {
        expand (version: project.version, mcVersion: mcVersion)
    }
}
